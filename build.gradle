plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'pmd'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file('config/checkstyle/checkstyle.xml')
    configProperties = [
        'baseDir': project.projectDir.absolutePath
    ]
    maxWarnings = 0
    ignoreFailures = false
}

pmd {
    toolVersion = '6.55.0'
    consoleOutput = true
    rulesMinimumPriority = 5
    ruleSetFiles = files('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

check.dependsOn checkstyleMain
check.dependsOn checkstyleTest
check.dependsOn pmdMain
check.dependsOn pmdTest

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.runelite.net'
    }
    mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
    compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion
    implementation group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '4.1.0', {
        exclude group: 'com.google.inject'
    }

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

group = 'com.duckblade.osrs'
version = '2.3.0'
sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}